set loadpath "~/.config/gnuplot/:/home/noxbru/.config/gnuplot/palettes"

load "dark.cfg"

# please, if I load a palette, use it
set style increment user

# Try to fix the ugly format that happens in logarithmic scales
set format x "%h"
set format y "%h"

# Some utils

min(x, y) = x < y ? x : y
max(x, y) = x > y ? x : y

min3(x, y, z) = min(x, min(y, z))
max3(x, y, z) = max(x, max(y, z))

cbrt(x) = x**(1/3.)

bin(x, w) = w * floor(x / w) + w/2.0

lerp(x, a, b) = (1 - x) * a + x * b
lerpx(x, x0, x1, y0, y1) = lerp((x - x0) / (x1 - x0), y0, y1)

power_law(x, x_min, x_max, p) = x < x_min ? 1/0 : \
                                x > x_max ? 1/0 : \
                                (1 - p) / (x_max**(1-p) - x_min**(1-p)) * x**-p

t_sign(x,y, x1,y1, x2,y2) = ((x  - x2) * (y1 - y2) - \
                             (x1 - x2) * (y  - y2)) < 0
inside_triangle(x,y, x1,y1, x2,y2, x3,y3) = (t_sign(x,y, x1,y1, x2,y2) == t_sign(x,y, x2,y2, x3,y3) && \
                                             t_sign(x,y, x2,y2, x3,y3) == t_sign(x,y, x3,y3, x1,y1))

gauss(x, mu, sigma) = 1 / (sqrt(2 * pi * sigma**2)) * exp( - (x - mu)**2 / (2 * sigma**2))
