# $Id$
# Maintainer: Daniel Micay <danielmicay@gmail.com>

pkgname=('termite' 'termite-terminfo')
pkgver=12
pkgrel=1
arch=('i686' 'x86_64')
url="https://github.com/thestinger/termite/"
license=('LGPL')
makedepends=('git' 'vte3-ng' 'ncurses')
source=("git://github.com/thestinger/termite#tag=v$pkgver"
        "0001-Remove-Ctrl-Shift-t-as-a-keybinding.patch")
md5sums=('SKIP' 'ac7028f876a7b02c3c3382ed00f60e6d')

build() {
  cd $pkgbase
  patch -i ../0001-Remove-Ctrl-Shift-t-as-a-keybinding.patch
  git submodule update --init
  make
}

package_termite() {
  pkgdesc="A simple VTE-based terminal"
  depends=('vte3-ng' 'termite-terminfo')
  backup=(etc/xdg/termite/config)

  cd $pkgbase
  make PREFIX=/usr DESTDIR="$pkgdir" install
  rm -rf "$pkgdir/usr/share/terminfo"
  install -Dm644 config "$pkgdir/etc/xdg/termite/config"
}

package_termite-terminfo() {
  pkgdesc='Terminfo for Termite, a simple VTE-based terminal'

  cd $pkgbase
  install -d "$pkgdir/usr/share/terminfo"
  tic -x termite.terminfo -o "$pkgdir/usr/share/terminfo"
}
